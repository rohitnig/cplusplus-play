dnl Process this file with autoconf to produce a configure script.
AC_INIT(hsocket,0.4.0)
AM_INIT_AUTOMAKE

###################################################################################################
# for socket library
###################################################################################################

AM_CONFIG_HEADER(config.h)

dnl Checks for programs.

AC_PROG_CXX
AC_PROG_RANLIB
WARN_ALL_OPTION="-Wall -Werror"

dnl Checks for libraries.

dnl Checks for header files.
AC_LANG_C
AC_CHECK_HEADERS( sys/time.h signal.h arpa/inet.h netinet/in.h sys/un.h sys/socket.h sys/wait.h netdb.h sys/sockio.h linux/sockios.h net/if.h netinet/in.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T

AC_LANG_CPLUSPLUS
AC_CHECK_TYPE( socklen_t,,
[
	saved_CXXFLAGS="$CXXFLAGS"
	AC_MSG_CHECKING(socklen_t type checking...)
	CXXFLAGS="$CXXFLAGS $WARN_ALL_OPTION"
	AC_TRY_COMPILE( 
		[
			#define _XOPEN_SOURCE_EXTENDED
			#include<sys/types.h>
			#include<sys/socket.h>
		],
		[
			struct sockaddr a;
			unsigned int socklen;
			accept( 0, & a, & socklen );
		],
		[
			AC_DEFINE(socklen_t, unsigned int )
			AC_MSG_RESULT(unsigned int)
		],
		[
			AC_DEFINE(socklen_t, int )
			AC_MSG_RESULT(int)
		])
	CXXFLAGS="$saved_CXXFLAGS"
],
[
	#include<sys/types.h>
	#include<sys/socket.h>
])
AH_TEMPLATE(socklen_t)


###################################################################################################
# for mangchi
###################################################################################################

dnl Checks for libraries.
AC_CHECK_LIB(socket, socket,
	[
		LIBSOCKET=-lsocket
	])

AC_CHECK_LIB(nsl, gethostbyname,
	[
		LIBNSL=-lnsl
	])
AC_SUBST(LIBSOCKET)
AC_SUBST(LIBNSL)


dnl Checks for library functions.
AC_OUTPUT(Makefile)
